package ItemsRules3

#imports
import com.sapienter.jbilling.server.item.PricingField
import com.sapienter.jbilling.server.mediation.task.MediationResult
import com.sapienter.jbilling.server.item.db.ItemDTO
import com.sapienter.jbilling.server.order.db.OrderLineDTO;
import com.sapienter.jbilling.server.order.db.OrderLineDAS;
import com.sapienter.jbilling.server.order.db.OrderDTO;
import com.sapienter.jbilling.server.item.tasks.SubscriptionResult
import com.sapienter.jbilling.server.order.OrderLineBL;
import com.sapienter.jbilling.server.order.OrderBL;
import java.math.BigDecimal;
import java.util.List;
import org.apache.log4j.Logger;

# globals
global org.apache.log4j.Logger LOG;

function List getOrderLines(Integer userId, Integer itemId) {
    return new OrderLineDAS().findByUserItem(userId, itemId);
}


rule "persistence control"
salience 65000 # run first
no-loop        # run once
    when
        # TODO: need a better way to control persistence; gui item management persist = false, mediation persist = true
        # no mediation result, assume that we're not operating as part
        # of a mediation process, but as normal GUI interaction
        not( exists( MediationResult( ) ) )
    then
        insert( new MediationResult("item management", false) );
        LOG.debug("Set persistence control to false, item rules running from GUI");
end

rule "fetch lemonade lovers plan subscription"
dialect 'java'
no-loop
    when
        $order : OrderDTO( )
        not( exists( SubscriptionResult( userId == $order.userId, itemId == 2702 ) ) )
    then
        insert( new SubscriptionResult($order, 2702) ); 
end

rule "Lemon and Coffee discount"
	when
        $order : OrderDTO( )    # any order
        OrderLineDTO( itemId == 2) from $order.lines # Lemonade
        OrderLineDTO( itemId == 3) from $order.lines # Coffee
        # only if the discount is not already there
        # otherwise an infinite loop happens (the no-loop attribute would
        # have the same effect)
        not ( OrderLineDTO( itemId == 14) from $order.lines )

        # persistence
        $result : MediationResult( )
	then
        OrderLineBL.addItem($order, 14, $result.getPersist()); # 10% discount
        update($order);
end


rule "Lemonade Plan"
    when
        $order : OrderDTO( )    # any order
        $planLine : OrderLineDTO( itemId == 250) from $order.lines # Plan

        # persistence
        $result : MediationResult( )
    then
        $order.getLines().remove($planLine); # Plan is only for grouping
        OrderLineBL.addItem($order, 1, $result.getPersist()); # The monthly lemonade subscription
        update($order);
        # now add an new order
        OrderDTO newOrder = OrderBL.createAsWithLine($order, 251, 1.0);  # The setup fee
        newOrder.setOrderPeriodId(1); # It should be one time
        newOrder.setNotes("As part of plan. ");
        insert(newOrder);
end

rule "bundle lemonade plan - request item"
no-loop # run only once
salience 10 # run before the plan rules
	when
        $order : OrderDTO( orderPeriod.id == 1 )    # any one-time order
        OrderLineDTO( itemId == 2600) from $order.lines # Generic lemonade
	then
        List<OrderLineDTO> lines = getOrderLines($order.getUserId(), 1); #request item 1: the plan
        for (OrderLineDTO line: lines) {
            insert(line);
            LOG.debug("inserted for user " + $order.getUserId() + " new line on request " + line);
        }
end

rule "bundle lemonade plan - included units"
salience 0 # run after the request rule
	when
        $order : OrderDTO( orderPeriod.id == 1 )    # any one-time order
        $generic : OrderLineDTO( itemId == 2600) from $order.lines # Generic lemonade
		OrderLineDTO( itemId == 1, purchaseOrder.userId == $order.userId) # plan

        # persistence
        $result : MediationResult( )
	then
        $order.getLines().remove($generic); # remove the generic
        OrderLineBL.addItem($order, 2601, $result.getPersist()); # add the included in plan
        update($order);
        LOG.debug("removed the generic lemonade and added the included in plan");
end

rule "bundle lemonade plan - not included units"
salience 0 # run after the request rule
	when
        $order : OrderDTO( orderPeriod.id == 1 )    # any one-time order
        $generic : OrderLineDTO( itemId == 2600) from $order.lines # Generic lemonade
		not OrderLineDTO( itemId == 1, purchaseOrder.userId == $order.userId) # plan

        # persistence
        $result : MediationResult( )
	then
        $order.getLines().remove($generic); # remove the generic
        OrderLineBL.addItem($order, 2602, $result.getPersist()); # add the normal item
        update($order);
        LOG.debug("removed the generic lemonade and added the priced item");
end

rule "lemonade lovers plan first 1000 included"
dialect 'java'
    when
        $order : OrderDTO( orderPeriod.id == 1 ) # any one-time order
        SubscriptionResult( userId == $order.userId, itemId == 2702, subscribed == true ) # Lemonade Lovers Plan

        $lemonade : OrderLineDTO( itemId == 2602 ) from $order.lines                       # Lemonade
        not( OrderLineDTO( itemId == 2601, quantity.intValue >= 1000 ) from $order.lines ) # Lemonade included not over 1000 units

        # persistence
        $result : MediationResult( )
    then
        # total already included
        BigDecimal included = BigDecimal.ZERO;
        for (OrderLineDTO line : $order.getLines()) {
            if (line.getItemId() == 2601)
                included.add(line.getQuantity());
        }

        BigDecimal maximum = new BigDecimal("1000");
        BigDecimal total = included.add($lemonade.getQuantity());
        if (total.compareTo(maximum) >= 0) {
            # max out included lemonade, leave the remaining units as regular lemonade to be billed
            OrderLineBL.addItem($order, 2601, maximum.subtract(included), $result.getPersist());
            $lemonade.setQuantity(total.subtract(maximum));
        } else {
            # add entire quantity as included, remove all regular lemonade from the order
            OrderLineBL.addItem($order, 2601, $lemonade.getQuantity(), $result.getPersist());
            $order.getLines().remove($lemonade);
        }

        update($order);
end


rule 'rateOrder test1'
	dialect "java"
when
	$field : PricingField( name == "add" )
	$order  : OrderDTO( )
then
    for (OrderLineDTO line: $order.getLines()) {
        line.setPrice(line.getPrice().add(new BigDecimal($field.getDoubleValue())));
    }
end

rule 'rateOrder test2'
	dialect "java"
when
	$field : PricingField( name == "subtract" )
	$order  : OrderDTO( )
then
    for (OrderLineDTO line: $order.getLines()) {
        line.setPrice(line.getPrice().subtract(new BigDecimal( $field.getDoubleValue())));
    }
end
